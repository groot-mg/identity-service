import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id "com.gorylenko.gradle-git-properties" version '2.4.1'
    id 'pl.allegro.tech.build.axion-release' version '1.15.3'
    id 'io.freefair.lombok'
    id 'java'
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.8"
}

ext {
    keycloakAdminClientVersion = '21.1.2'
    openApiVersion = '1.7.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

scmVersion {
    tag { prefix = rootProject.name + '-' }
    versionIncrementer 'incrementMinor'
}
project.version = scmVersion.version

def commonJarConfigClosure = {
    archiveName = "${rootProject.name}.jar"
}

bootJar commonJarConfigClosure

springBoot {
    buildInfo {
        properties {
            name = rootProject.name
            version = project.version
            additional = [
                    host           : InetAddress.localHost.hostName,
                    hostAddress    : InetAddress.localHost.getHostAddress(),
                    operatingSystem: DefaultNativePlatform.currentOperatingSystem.getInternalOs().toString()
            ]
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "org.keycloak:keycloak-admin-client:${keycloakAdminClientVersion}"
    implementation "org.springdoc:springdoc-openapi-ui:${openApiVersion}"

    implementation 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/generoso/identity/IdentityApplication.class"
            ])
        }))
    }
    reports {
        xml.enabled true
    }
}